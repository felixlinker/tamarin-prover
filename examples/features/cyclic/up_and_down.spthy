theory UpAndDown
begin

functions: h/1

rule Start:
  [ Fr(~x) ]
  --[ Gen(~x, ~x) ]->
  [ Up(~x, ~x) ]

rule Up:
  [ Up(id, x) ]
  --[ Gen(id, h(x)) ]->
  [ Up(id, h(x)) ]

rule DownStart:
  [ Up(idUp, x), Fr(~idDown) ]
  --[ Destr(x) ]->
  [ Down(~idDown, x) ]

rule Down:
  [ Down(id, h(x)) ]
  --[ Destr(x) ]->
  [ Down(id, x) ]

rule DownEnd:
    // Note: If we replace x with ~x, the lemma would also not be provable after
    // searching for backlinks under substitutions.
  [ Down(id, x) ]
  --[ End(x) ]->
  []

lemma CorrectnessInvariant[reuse]:
  "All x #a. Destr(x) @ #a ==> Ex id #b. Gen(id, x) @ #b"

lemma CorrectnessEnd[hide_lemma=CorrectnessInvariant]:
  "All x #a. End(x) @ #a ==> Ex id #b. Gen(id, x) @ #b"

lemma GenUnique[reuse,hide_lemma=CorrectnessInvariant]:
  "All id1 id2 x #a #b. Gen(id1, x) @ #a & Gen(id2, x) @ #b ==> #a = #b"

lemma GenInvariant[reuse,hide_lemma=CorrectnessInvariant]:
  "All id1 id2 x y #a #b. Gen(id1, x) @ #a & Gen(id2, y) @ #b & (x << y | x = y) ==> id1 = id2"

// Not provable
lemma Correspondence:
  "All x #a #b. Destr(x) @ #a & Destr(x) @ #b ==> #a = #b"

end
